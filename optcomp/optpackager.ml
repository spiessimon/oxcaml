(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*             Xavier Leroy, projet Cristal, INRIA Rocquencourt           *)
(*                                                                        *)
(*   Copyright 2002 Institut National de Recherche en Informatique et     *)
(*     en Automatique.                                                    *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

(* "Package" a set of .cmx/.o files into one .cmx/.o file having the original
   compilation units as sub-modules. *)

open Misc
open Cmx_format
module CU = Compilation_unit

module type S = sig
  val package_files :
    ppf_dump:Format.formatter -> Env.t -> string list -> string -> unit
end

type error =
  | Illegal_renaming of CU.Name.t * string * CU.Name.t
  | Forward_reference of string * CU.Name.t
  | Wrong_for_pack of string * CU.t
  | Assembler_error of string
  | File_not_found of string
  | Pack_with_support_lto
  | Member_with_lto_info of string

exception Error of error

module Make (Backend : sig
  include Optcomp_intf.Backend

  include Optlink.S
end) : S = struct
  (* Read the unit information from a .cmx file. *)

  type pack_member_kind =
    | PM_intf
    | PM_impl of unit_infos

  type pack_member =
    { pm_file : string;
      pm_name : CU.Name.t;
      pm_kind : pack_member_kind
    }

  let read_member_info linkenv pack_path file =
    let for_pack_prefix = CU.to_prefix pack_path in
    let unit_info = Unit_info.Artifact.from_filename ~for_pack_prefix file in
    let name = Unit_info.Artifact.modname unit_info |> CU.name in
    let kind =
      if Unit_info.is_cmi unit_info
      then PM_intf
      else
        let info, crc = Compilenv.read_unit_info file in
        if not (CU.Name.equal (CU.name info.ui_unit) name)
        then raise (Error (Illegal_renaming (name, file, CU.name info.ui_unit)));
        if not (CU.is_parent pack_path ~child:info.ui_unit)
        then raise (Error (Wrong_for_pack (file, pack_path)));
        (* Packing would drop the LTO sections (see [build_package_cmx]), so the
           packed unit could not take part in a Reaper solve. *)
        if Option.is_some info.ui_lto_info
        then raise (Error (Member_with_lto_info file));
        Backend.check_consistency linkenv file info crc;
        Compilenv.cache_unit_info info;
        PM_impl info
    in
    { pm_file = file; pm_name = name; pm_kind = kind }

  (* Check absence of forward references *)

  let check_units members =
    let rec check forbidden = function
      | [] -> ()
      | mb :: tl ->
        (match mb.pm_kind with
        | PM_intf -> ()
        | PM_impl infos ->
          List.iter
            (fun import ->
              let unit = Import_info.cu import in
              let name = CU.name unit in
              if List.mem name forbidden
              then raise (Error (Forward_reference (mb.pm_file, name))))
            infos.ui_imports_cmx);
        check (list_remove mb.pm_name forbidden) tl
    in
    check (List.map (fun mb -> mb.pm_name) members) members

  let make_package_object ~ppf_dump members target coercion =
    let pack_name =
      Printf.sprintf "pack(%s)"
        (Unit_info.Artifact.modname target |> CU.name_as_string)
    in
    Profile.record_call pack_name (fun () ->
        let objtemp =
          if !Clflags.keep_asm_file
          then Unit_info.Artifact.prefix target ^ ".pack" ^ Config.ext_obj
          else
            (* Put the full name of the module in the temporary file name to
               avoid collisions with MSVC's link /lib in case of successive
               packs *)
            let name =
              Current_unit.symbol () |> Symbol.linkage_name
              |> Linkage_name.to_string
            in
            Filename.temp_file name Config.ext_obj
        in
        let components =
          List.map
            (fun m ->
              match m.pm_kind with
              | PM_intf -> None
              | PM_impl _ ->
                Some (CU.create_child (Current_unit.get_cu_exn ()) m.pm_name))
            members
        in
        let compilation_unit = Unit_info.Artifact.modname target in
        let prefixname = Filename.remove_extension objtemp in
        let required_globals = Compilation_unit.Set.empty in
        let main_module_block_size, code =
          Translmod.transl_package components coercion
        in
        let code =
          Simplif.simplify_lambda code
            ~restrict_to_upstream_dwarf:!Clflags.restrict_to_upstream_dwarf
            ~gdwarf_may_alter_codegen:!Dwarf_flags.gdwarf_may_alter_codegen
        in
        let main_module_block_format : Lambda.main_module_block_format =
          Mb_struct
            { mb_repr =
                Module_value_only { field_count = main_module_block_size }
            }
        in
        let arg_block_idx =
          (* Packs not supported as argument modules *)
          None
        in
        let program =
          { Lambda.code;
            main_module_block_format;
            arg_block_idx;
            compilation_unit;
            required_globals
          }
        in
        Backend.compile_implementation ~keep_symbol_tables:true
          ~sourcefile:(Unit_info.Artifact.original_source_file target)
          ~prefixname ~ppf_dump program;
        let objfiles =
          List.map
            (fun m -> Filename.remove_extension m.pm_file ^ Config.ext_obj)
            (List.filter (fun m -> m.pm_kind <> PM_intf) members)
        in
        Misc.try_finally
          ~always:(fun () -> remove_file objtemp)
          (fun () ->
            Backend.link_partial
              (Unit_info.Artifact.filename target)
              (objtemp :: objfiles));
        main_module_block_format)

  let build_package_cmx linkenv members cmxfile main_module_block_format =
    let unit_names = List.map (fun m -> m.pm_name) members in
    let filter lst =
      List.filter
        (fun import -> not (List.mem (Import_info.name import) unit_names))
        lst
    in
    let union lst =
      List.fold_left
        (List.fold_left (fun accu n ->
             if List.mem n accu then accu else n :: accu))
        [] lst
    in
    let units =
      List.fold_right
        (fun m accu ->
          match m.pm_kind with PM_intf -> accu | PM_impl info -> info :: accu)
        members []
    in
    let ui =
      (* [arg_descr] is None because we don't allow packs to be arguments.
         [static_data] is empty as we don't support packs with layout poly. *)
      Compilenv.build_unit_info ~main_module_block_format ~arg_descr:None
        ~static_data:(Slambdaeval.CU_data.empty ())
    in
    let file_sections =
      let length =
        List.fold_left
          (fun acc info -> acc + File_sections.length info.ui_file_sections)
          0 (ui :: units)
      in
      File_sections.Builder.create length
    in
    let ui_export_info =
      Flambda2_cmx.Flambda_cmx_format.pack ~sections:file_sections
        (Compilenv.get_export_info ui
        :: List.map Compilenv.get_export_info units)
    in
    let ui_zero_alloc_info = Zero_alloc_info.create () in
    List.iter
      (fun info ->
        Zero_alloc_info.merge info.ui_zero_alloc_info ~into:ui_zero_alloc_info)
      units;
    let modname = Compilation_unit.name ui.ui_unit in
    let pkg_infos =
      { ui_unit = ui.ui_unit;
        ui_defines =
          List.flatten (List.map (fun info -> info.ui_defines) units)
          @ [ui.ui_unit];
        ui_arg_descr = None;
        ui_imports_cmi =
          Import_info.create modname
            ~crc_with_unit:(Some (ui.ui_unit, Env.crc_of_unit modname))
          :: filter (Linkenv.extract_crc_interfaces linkenv);
        ui_imports_cmx = filter (Linkenv.extract_crc_implementations linkenv);
        ui_quoted_cmi = union (List.map (fun info -> info.ui_quoted_cmi) units);
        ui_quoted_cmx = union (List.map (fun info -> info.ui_quoted_cmx) units);
        ui_format = ui.ui_format;
        ui_generic_fns =
          { curry_fun =
              union (List.map (fun info -> info.ui_generic_fns.curry_fun) units);
            apply_fun =
              union (List.map (fun info -> info.ui_generic_fns.apply_fun) units);
            send_fun =
              union (List.map (fun info -> info.ui_generic_fns.send_fun) units)
          };
        ui_force_link = List.exists (fun info -> info.ui_force_link) units;
        ui_requires_metaprogramming =
          List.exists (fun info -> info.ui_requires_metaprogramming) units;
        ui_export_info;
        ui_zero_alloc_info;
        ui_external_symbols =
          union (List.map (fun info -> info.ui_external_symbols) units);
        ui_static_data = ui.ui_static_data;
        (* Only the sections reachable from [ui_export_info] are copied above,
           and [read_member_info] rejects members with LTO sections. *)
        ui_lto_info = None;
        ui_file_sections = File_sections.Builder.build file_sections
      }
    in
    Compilenv.write_unit_info pkg_infos cmxfile

  let package_object_files ~ppf_dump files target targetcmx coercion =
    let pack_path = Unit_info.Artifact.modname target in
    let linkenv = Linkenv.create () in
    let members = map_left_right (read_member_info linkenv pack_path) files in
    check_units members;
    let main_module_block_format =
      make_package_object ~ppf_dump members target coercion
    in
    build_package_cmx linkenv members targetcmx main_module_block_format

  (* The entry point *)

  let package_files ~ppf_dump initial_env files targetcmx =
    let files =
      List.map
        (fun f ->
          try Load_path.find f
          with Not_found -> raise (Error (File_not_found f)))
        files
    in
    let for_pack_prefix = CU.Prefix.from_clflags () in
    let cmx = Unit_info.Artifact.from_filename ~for_pack_prefix targetcmx in
    let cmi = Unit_info.companion_cmi cmx in
    let obj = Unit_info.companion_obj cmx in
    (* Set the name of the current "input" *)
    Location.input_name := targetcmx;
    (* Set the name of the current compunit *)
    let unit_info =
      Unit_info.of_artifact Impl cmx ~dummy_source_file:targetcmx
    in
    let comp_unit = Unit_info.Artifact.modname cmx in
    if Flambda2_ui.Flambda_features.support_lto ()
    then raise (Error Pack_with_support_lto);
    Compilenv.reset unit_info;
    Misc.try_finally
      (fun () ->
        let coercion = Typemod.package_units initial_env files cmi comp_unit in
        package_object_files ~ppf_dump files obj targetcmx coercion)
      ~exceptionally:(fun () ->
        remove_file targetcmx;
        remove_file (Unit_info.Artifact.filename obj))
end

(* Error report *)

open Format_doc
module Style = Misc.Style

let report_error ppf = function
  | Illegal_renaming (name, file, id) ->
    fprintf ppf
      "Wrong file naming: %a@ contains the code for@ %a when %a was expected"
      Location.Doc.quoted_filename file CU.Name.print_as_inline_code name
      CU.Name.print_as_inline_code id
  | Forward_reference (file, ident) ->
    fprintf ppf "Forward reference to %a in file %a"
      CU.Name.print_as_inline_code ident Location.Doc.quoted_filename file
  | Wrong_for_pack (file, path) ->
    fprintf ppf "File %a@ was not compiled with the `-for-pack %a' option"
      Location.Doc.quoted_filename file CU.print_as_inline_code path
  | File_not_found file ->
    fprintf ppf "File %a not found" Style.inline_code file
  | Assembler_error file ->
    fprintf ppf "Error while assembling %a" Style.inline_code file
  | Pack_with_support_lto ->
    fprintf ppf "%a is not supported with %a" Style.inline_code "-pack"
      Style.inline_code "-support-lto"
  | Member_with_lto_info file ->
    fprintf ppf "File %a@ was compiled with %a and cannot be packed"
      Location.Doc.quoted_filename file Style.inline_code "-support-lto"

let () =
  Location.register_error_of_exn (function
    | Error err -> Some (Location.error_of_printer_file report_error err)
    | _ -> None)
