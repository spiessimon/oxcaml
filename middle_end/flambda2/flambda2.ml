(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*                       Pierre Chambart, OCamlPro                        *)
(*           Mark Shinwell and Leo White, Jane Street Europe              *)
(*                                                                        *)
(*   Copyright 2013--2021 OCamlPro SAS                                    *)
(*   Copyright 2014--2021 Jane Street Group LLC                           *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

(* Unlike most of the rest of Flambda 2, this file depends on ocamloptcomp,
   meaning it can call [Compilenv]. *)

let get_module_info comp_unit =
  let cmx_name = Compilation_unit.name comp_unit in
  (* Typing information for predefined exceptions should be populated directly
     by the callee. *)
  if Compilation_unit.Name.equal cmx_name Compilation_unit.Name.predef_exn
  then
    Misc.fatal_error
      "get_global_info is not for use with predefined exception compilation \
       units";
  if
    Compilation_unit.Name.equal cmx_name
      (Flambda2_identifiers.Symbol.external_symbols_compilation_unit ()
      |> Compilation_unit.name)
  then None
  else Compilenv.get_unit_export_info comp_unit

let dump_to_target_if_any main_dump_ppf target ~header ~f a =
  match (target : Flambda_features.dump_target) with
  | Nowhere -> ()
  | Main_dump_stream ->
    Format.fprintf main_dump_ppf "\n%t%s:%t@ %a@." Flambda_colours.each_file
      header Flambda_colours.pop f a
  | File filename ->
    Misc.protect_output_to_file filename (fun out ->
        let ppf = Format.formatter_of_out_channel out in
        f ppf a;
        Format.pp_print_flush ppf ())

let dump_if_enabled ppf enabled ~header ~f a =
  let target : Flambda_features.dump_target =
    if enabled then Main_dump_stream else Nowhere
  in
  dump_to_target_if_any ppf target ~header ~f a

let pp_flambda_as_fexpr ppf unit =
  Print_fexpr.flambda_unit ppf (unit |> Flambda_to_fexpr.conv)

let dump_fexpr_annot ~prefixname suffix unit =
  let dump =
    Flambda_features.dump_fexpr_annot ()
    || List.exists (String.equal suffix)
         (Flambda_features.dump_fexpr_annot_after ())
  in
  if dump
  then
    Misc.protect_output_to_file
      (prefixname ^ "." ^ suffix ^ ".fl")
      (fun out ->
        let ppf = Format.formatter_of_out_channel out in
        pp_flambda_as_fexpr ppf unit;
        Format.pp_print_flush ppf ())

let print_rawflambda ppf unit =
  dump_if_enabled ppf
    (Flambda_features.dump_rawflambda ())
    ~header:"After CPS conversion" ~f:Flambda_unit.print unit;
  dump_to_target_if_any ppf
    (Flambda_features.dump_rawfexpr ())
    ~header:"After CPS conversion" ~f:pp_flambda_as_fexpr unit

let print_flambda name condition ppf unit =
  let header = "After " ^ name in
  dump_if_enabled ppf condition ~header ~f:Flambda_unit.print unit

let print_fexpr name target ppf unit =
  let header = "After " ^ name in
  dump_to_target_if_any ppf target ~header ~f:pp_flambda_as_fexpr unit

module NO = Flambda2_nominal.Name_occurrences

type run_result =
  { cmx : Flambda_cmx_format.raw option;
    unit : Flambda_unit.t;
    all_code : Exported_code.t;
    exported_offsets : Exported_offsets.t;
    reachable_names : NO.t
  }

let build_run_result unit ~prepare_cmx ~all_code
    ({ used_value_slots; exported_offsets } : Slot_offsets.result) : run_result
    =
  let module_symbol = Flambda_unit.module_symbol unit in
  let reachable_names, cmx =
    prepare_cmx ~module_symbol ~used_value_slots ~exported_offsets all_code
  in
  { cmx; unit; all_code; exported_offsets; reachable_names }

type flambda_result =
  { flambda : Flambda_unit.t;
    all_code : Exported_code.t;
    offsets : Exported_offsets.t;
    reachable_names : NO.t
  }

let finalize_offsets ~free_names ~all_code slot_offsets =
  Slot_offsets.finalize_offsets_from_free_names slot_offsets
    ~get_code_metadata:(Exported_code.get_code_metadata all_code)
    ~free_names

let run_reaper ~ppf ~prefixname ~machine_width ~cmx_loader ~all_code
    ~final_typing_env ~free_names flambda =
  let ((flambda, _, _, _) as result) =
    Profile.record_call ~accumulate:true "reaper" (fun () ->
        Flambda2_reaper.Reaper.run ~machine_width ~cmx_loader ~all_code
          ~final_typing_env ~free_names flambda)
  in
  print_flambda "reaper" (Flambda_features.dump_reaper ()) ppf flambda;
  print_fexpr "reaper"
    (Flambda_features.dump_fexpr (This_pass "reaper"))
    ppf flambda;
  dump_fexpr_annot ~prefixname "reaper" flambda;
  Compiler_hooks.execute Reaped_flambda2 flambda;
  result

let compilation_unit_callbacks = ref []

let register_compilation_unit_callback f =
  compilation_unit_callbacks := f :: !compilation_unit_callbacks

let invoke_compilation_unit_callbacks res =
  List.iter (( |> ) res) !compilation_unit_callbacks;
  compilation_unit_callbacks := []

module Reaper_mode = struct
  (* CR mvellacott: in the future it would be nice to allow running the Reaper
     on the present unit and supporting LTO at the same time, but at the moment
     it isn't safe to run the Reaper twice on the same code. *)
  type t =
    | Single_unit_run
    | Lto_support
    | Disabled

  let of_flags () =
    if Flambda_features.support_lto ()
    then Lto_support
    else if Flambda_features.enable_reaper ()
    then Single_unit_run
    else Disabled
end

let reaper_oclassic = Oxcaml_args.Extra_options.bool __LOC__ "reaper-oclassic"

let flambda_to_flambda0 : type m.
    ppf_dump:Format.formatter ->
    prefixname:string ->
    cmx_loader:Flambda_cmx.loader ->
    machine_width:Target_system.Machine_width.t ->
    mode:m Flambda_features.mode ->
    close_prog_metadata:m Closure_conversion.close_program_metadata ->
    code_slot_offsets:Slot_offsets.t Flambda2_identifiers.Code_id.Map.t ->
    sections:File_sections.Builder.t ->
    Flambda_unit.t ->
    flambda_result =
 fun ~ppf_dump:ppf ~prefixname ~cmx_loader ~machine_width ~mode
     ~close_prog_metadata ~code_slot_offsets ~sections raw_flambda ->
  Compiler_hooks.execute Raw_flambda2 raw_flambda;
  print_rawflambda ppf raw_flambda;
  dump_fexpr_annot ~prefixname "raw" raw_flambda;
  let flambda, all_code, slot_offsets, prepare_cmx, last_pass_name, lto_sections
      =
    match mode, close_prog_metadata with
    | Classic, Classic (all_code, approxs, free_names, slot_offsets) ->
      (if Flambda_features.inlining_report ()
       then
         let output_prefix = prefixname ^ ".cps_conv" in
         let inlining_tree =
           Inlining_report.output_then_forget_decisions ~output_prefix
         in
         Compiler_hooks.execute Inlining_tree inlining_tree);
      if Flambda_features.enable_reaper () && reaper_oclassic ()
      then
        (* The reaper needs to rewrite the typing environment, so we need to
           convert the value approximations to a real typing environment. *)
        let final_typing_env =
          Flambda2_types.Typing_env.create_from_closure_conversion_approx
            ~machine_width
            ~resolver:(Flambda_cmx.load_cmx_file_contents cmx_loader)
            approxs
        in
        let flambda, all_code, slot_offsets, final_typing_env =
          run_reaper ~ppf ~prefixname ~machine_width ~cmx_loader ~all_code
            ~final_typing_env:(Some final_typing_env) ~free_names raw_flambda
        in
        let prepare_cmx ~module_symbol ~used_value_slots ~exported_offsets
            all_code =
          Flambda_cmx.prepare_cmx_file_contents ~final_typing_env ~module_symbol
            ~used_value_slots ~exported_offsets ~sections all_code
        in
        flambda, all_code, slot_offsets, prepare_cmx, "reaper", None
      else
        let slot_offsets =
          finalize_offsets ~free_names ~all_code slot_offsets
        in
        let prepare_cmx ~module_symbol ~used_value_slots ~exported_offsets
            all_code =
          Flambda_cmx.prepare_cmx_from_approx ~machine_width ~approxs
            ~module_symbol ~exported_offsets ~used_value_slots ~sections
            all_code
        in
        raw_flambda, all_code, slot_offsets, prepare_cmx, "raw", None
    | Normal, Normal ->
      let round = 0 in
      let { Simplify.free_names;
            final_typing_env;
            all_code;
            slot_offsets;
            unit = flambda
          } =
        Profile.record_call ~accumulate:true "simplify" (fun () ->
            Simplify.run ~cmx_loader ~machine_width ~round ~code_slot_offsets
              raw_flambda)
      in
      (if Flambda_features.inlining_report ()
       then
         let output_prefix = Printf.sprintf "%s.%d" prefixname round in
         let inlining_tree =
           Inlining_report.output_then_forget_decisions ~output_prefix
         in
         Compiler_hooks.execute Inlining_tree inlining_tree);
      Compiler_hooks.execute Flambda2 flambda;
      let last_pass_name = "simplify" in
      print_flambda last_pass_name
        (Flambda_features.dump_simplify ())
        ppf flambda;
      print_fexpr "simplify"
        (Flambda_features.dump_fexpr (This_pass "simplify"))
        ppf flambda;
      dump_fexpr_annot ~prefixname "simplify" flambda;
      let ( (flambda, all_code, slot_offsets, final_typing_env),
            last_pass_name,
            lto_sections ) =
        match Reaper_mode.of_flags () with
        | Disabled ->
          let slot_offsets =
            finalize_offsets ~free_names ~all_code slot_offsets
          in
          ( (flambda, all_code, slot_offsets, final_typing_env),
            last_pass_name,
            None )
        | Single_unit_run ->
          let result =
            run_reaper ~ppf ~prefixname ~machine_width ~cmx_loader ~all_code
              ~final_typing_env ~free_names flambda
          in
          result, "reaper", None
        | Lto_support ->
          let deps, slot_offsets_inputs, solve_inputs, rebuild_data =
            Flambda2_reaper.Reaper.Staged.traverse ~free_names ~cmx_loader
              ~all_code ~closed_world:true flambda
          in
          let lto_sections =
            Flambda2_reaper.Lto_sections.create
              ~unit_metadata:(Flambda_unit.metadata flambda)
              ~imported_offsets:(Exported_offsets.imported_offsets ())
              ~deps ~slot_offsets_inputs ~solve_inputs ~rebuild_data
          in
          let slot_offsets =
            finalize_offsets ~free_names ~all_code slot_offsets
          in
          ( (flambda, all_code, slot_offsets, final_typing_env),
            last_pass_name,
            Some lto_sections )
      in
      (* The LTO sections are renamed on import with the table of the export
         information, so their identifiers must be exported too. *)
      let lto_ids =
        match lto_sections with
        | None -> Flambda2_nominal.Ids_for_export.empty
        | Some lto_sections ->
          Flambda2_reaper.Lto_sections.ids_for_export lto_sections
      in
      let prepare_cmx ~module_symbol ~used_value_slots ~exported_offsets
          all_code =
        Flambda_cmx.prepare_cmx_file_contents ~lto_ids ~final_typing_env
          ~module_symbol ~used_value_slots ~exported_offsets ~sections all_code
      in
      flambda, all_code, slot_offsets, prepare_cmx, last_pass_name, lto_sections
  in
  print_flambda last_pass_name (Flambda_features.dump_flambda ()) ppf flambda;
  print_fexpr last_pass_name (Flambda_features.dump_fexpr Last_pass) ppf flambda;
  let { unit = flambda; exported_offsets; cmx; all_code; reachable_names } =
    build_run_result flambda ~all_code slot_offsets ~prepare_cmx
  in
  (match cmx with
  | None -> () (* Opaque compilation *)
  | Some cmx -> Compilenv.set_export_info cmx);
  (match lto_sections, cmx with
  | None, _ -> ()
  | Some lto_sections, Some _ ->
    Compilenv.set_lto_info
      (Flambda2_reaper.Lto_sections.to_sections ~sections lto_sections)
  | Some _, None ->
    (* The export record is only omitted for -opaque, which [lambda_to_flambda]
       rejects together with -support-lto. *)
    Misc.fatal_error
      "-support-lto with -opaque should have been rejected before reaching \
       Flambda 2");
  { flambda; offsets = exported_offsets; reachable_names; all_code }

let flambda_to_flambda ~ppf_dump ~prefixname ~machine_width ~code_slot_offsets
    (unit : Flambda_unit.t) =
  (* CR bclement: this does not seem like the right place to set this up. *)
  Misc.Style.setup (Flambda_features.colour ());
  let cmx_loader = Flambda_cmx.create_loader ~get_module_info in
  let mode, close_prog_metadata =
    match Flambda_features.mode () with
    | Mode Normal -> Flambda_features.Normal, Closure_conversion.Normal
    | Mode Classic ->
      Misc.fatal_error "Unsupported classic mode in standalone middle-end pass"
  in
  let sections = Compilenv.current_sections () in
  flambda_to_flambda0 ~ppf_dump ~prefixname ~cmx_loader ~machine_width ~mode
    ~close_prog_metadata ~code_slot_offsets ~sections unit

let lambda_to_flambda ~ppf_dump:ppf ~prefixname ~machine_width
    (program : Lambda.program) =
  let module_repr =
    Lambda.main_module_representation program.main_module_block_format
  in
  let compilation_unit = program.compilation_unit in
  let module_initializer = program.code in
  (* Make sure -linscan is enabled in classic mode. Doing this here to be sure
     it happens exactly when -Oclassic is in effect, which we don't know at CLI
     processing time because there may be an [@@@flambda_oclassic] or
     [@@@flambda_o3] attribute. *)
  if Flambda_features.classic_mode () then Clflags.use_linscan := true;
  Misc.Style.setup (Flambda_features.colour ());
  (* The LTO sections of the .cmx file are imported with the table of the export
     information, which -opaque omits. *)
  if Flambda_features.support_lto () && Flambda_features.opaque ()
  then
    Location.raise_errorf
      ~loc:(Location.in_file !Location.input_name)
      "-support-lto is incompatible with -opaque";
  (* CR-someday mshinwell: Note for future WebAssembly work: this thing about
     the length of arrays will need fixing, I don't think it only applies to the
     Cmm translation.

     This is partially fixed now, but the float array optimization case for
     array length in the Cmm translation assumes the floats are word width. *)
  (* The Flambda 2 code won't currently operate on 32-bit hosts; see
     [Name_occurrences]. *)
  if Sys.word_size <> 64
  then Misc.fatal_error "Flambda 2 can only run on 64-bit hosts at present";
  (* At least one place in the Cmm translation code (for unboxed arrays) cannot
     cope with big-endian systems, and it seems unlikely any such systems will
     have to be supported in the future anyway. *)
  if Arch.big_endian
  then Misc.fatal_error "Flambda2 only supports little-endian hosts";
  (* When the float array optimisation is enabled, the length of an array needs
     to be computed differently according to the array kind, in the case where
     the width of a float is not equal to the machine word width (at present,
     this happens only on 32-bit targets). *)
  if
    Cmm_helpers.wordsize_shift <> Cmm_helpers.numfloat_shift
    && Flambda_features.flat_float_array ()
  then
    Misc.fatal_error
      "Cannot compile on targets where floats are not word-width when the \
       float array optimisation is enabled";
  let cmx_loader = Flambda_cmx.create_loader ~get_module_info in
  let (Mode mode) = Flambda_features.mode () in
  let sections = Compilenv.current_sections () in
  let { Closure_conversion.unit = raw_flambda;
        code_slot_offsets;
        metadata = close_prog_metadata
      } =
    Profile.record_call "lambda_to_flambda" (fun () ->
        Lambda_to_flambda.lambda_to_flambda ~mode ~machine_width
          ~big_endian:Arch.big_endian ~cmx_loader ~compilation_unit ~module_repr
          module_initializer)
  in
  invoke_compilation_unit_callbacks compilation_unit;
  flambda_to_flambda0 ~ppf_dump:ppf ~prefixname ~cmx_loader ~machine_width ~mode
    ~close_prog_metadata ~code_slot_offsets ~sections raw_flambda

let reset_symbol_tables () =
  Compilenv.reset_info_tables ();
  Flambda2_identifiers.Continuation.reset ();
  Flambda2_identifiers.Int_ids.reset ()

let flambda_result_to_cmm ~keep_symbol_tables ~localise_unreachable_symbols
    ({ flambda; all_code; offsets; reachable_names } : flambda_result) =
  let cmm =
    Flambda2_to_cmm.To_cmm.unit flambda ~all_code ~offsets ~reachable_names
      ~localise_unreachable_symbols
  in
  if not keep_symbol_tables then reset_symbol_tables ();
  cmm

let lambda_to_cmm ~ppf_dump ~prefixname ~machine_width ~keep_symbol_tables
    (program : Lambda.program) =
  let run () =
    (* Keep unreachable symbols global whenever the Reaper is involved. The
       staged rebuild must (see [reaped_flambda2_to_cmm]); the other Reaper
       modes follow suit so that a staged rebuild of a unit produces the same
       object file as a direct Reaper compilation of it. *)
    let localise_unreachable_symbols =
      match Reaper_mode.of_flags () with
      | Disabled -> true
      | Single_unit_run | Lto_support -> false
    in
    lambda_to_flambda ~ppf_dump ~prefixname ~machine_width program
    |> flambda_result_to_cmm ~keep_symbol_tables ~localise_unreachable_symbols
  in
  Profile.record_call "flambda2" run

(* Read what the LTO entry points need from the .cmx file of a unit compiled
   with -support-lto. Creates no identifiers, so it may be called before the
   stamp counters are restored. *)
let read_lto_header_and_export_info ~filename
    (unit_infos : Cmx_format.unit_infos) =
  let header =
    Flambda2_reaper.Lto_sections.read_header ~filename
      ~sections:unit_infos.ui_file_sections unit_infos.ui_lto_info
  in
  let export_info =
    match Compilenv.get_export_info unit_infos with
    | Some export_info -> export_info
    | None ->
      Misc.fatal_errorf "%s has LTO information but no export information"
        filename
  in
  header, export_info

let reaper_lto_solve ~cmx_files ~ltosol_file =
  (* ID stamp counters are process-global monotonically increasing counters that
     give us an easy way of creating fresh identifiers. These identifiers get
     persisted across processes, and we need to prevent collisions when this
     happens. There are two cases:

     (1) Different processes that operate on different compilation units,
     potentially in parallel. Collisions are prevented here by the fact that
     identifiers are scoped to compilation units, as (CU, number) pairs.

     (2) Different processes that operate on the same compilation units, which
     must always happen in sequence. Collisions are prevented here by saving
     stamp counters in the earlier processes and restoring them in the later
     processes.

     Here we're resuming from many processes that operated on different CUs, and
     we're operating on all of those CUs, so we need to restore stamp counters
     from all of those processes. However stamp counters are global for the
     process, not per-CU. To make this work, we take the maximum across all the
     processes we've resumed from. It is okay that this makes some unused stamps
     get jumped over, the important thing is that they increase monotonically.

     After we're done, rebuild processes will be created to do more work on the
     CUs we touched. To keep counters monotonically increasing, we need to save
     them after our work so that the rebuild processes can restore them. *)
  let units =
    List.map
      (fun filename ->
        let unit_infos, (_ : Digest.t) = Compilenv.read_unit_info filename in
        let header, export_info =
          read_lto_header_and_export_info ~filename unit_infos
        in
        filename, unit_infos, header, export_info)
      cmx_files
  in
  Flambda2_reaper.Id_stamp_counters.restore_for_merge
    (List.map
       (fun (_, _, header, _) ->
         Flambda2_reaper.Lto_sections.Header.id_stamp_counters header)
       units);
  let solve_data =
    List.map
      (fun (filename, (unit_infos : Cmx_format.unit_infos), header, export_info)
         ->
        ( unit_infos.ui_unit,
          Flambda2_reaper.Lto_sections.read_for_solve ~filename
            ~sections:unit_infos.ui_file_sections ~export_info header ))
      units
  in
  let participants = List.map fst solve_data in
  let combined_graph =
    List.fold_left
      (fun combined (_participant, (graph, _, _, _)) ->
        Flambda2_reaper.Global_flow_graph.union combined graph)
      (Flambda2_reaper.Global_flow_graph.create ())
      solve_data
  in
  let slot_offsets_inputs =
    List.fold_left
      (fun combined (_participant, (_, inputs, _, _)) ->
        Flambda2_reaper.Slot_offsets_analysis.Inputs.union combined inputs)
      Flambda2_reaper.Slot_offsets_analysis.Inputs.empty solve_data
  in
  let solve_inputs =
    List.map
      (fun (_participant, (_, _, _, solve_inputs)) -> solve_inputs)
      solve_data
  in
  let participant_units = Compilation_unit.Set.of_list participants in
  let analysis_scope =
    Flambda2_reaper.Analysis.Scope.Lto_participants participant_units
  in
  (* Make the offsets of slots defined by units outside the solve available to
     [Slot_offsets.finalize_offsets]. The offsets of the participants' own slots
     are recomputed from the solution, so the stale ones stored in the .cmx
     files must not be imported. *)
  List.iter
    (fun (_participant, (_, _, imported_offsets, _)) ->
      Exported_offsets.import_offsets
        (Exported_offsets.filter_by_compilation_unit imported_offsets
           ~keep:(fun cu ->
             not
               (Flambda2_reaper.Analysis.Scope.contains_unit analysis_scope cu))))
    solve_data;
  let solution, slot_offsets =
    Flambda2_reaper.Reaper.Staged.solve ~slot_offsets_inputs ~analysis_scope
      ~solve_inputs combined_graph
  in
  Flambda2_reaper.Ltosol_format.save ~filename:ltosol_file ~participants
    ~solution ~slot_offsets

let reaped_flambda2_to_cmm ~machine_width ~ltosol_filename ~batch_members =
  (* Everything up to the function returned below is computed once and shared by
     the whole batch of rebuilds. *)
  let ltosol =
    Profile.record_call ~accumulate:true "ltosol_load" (fun () ->
        Flambda2_reaper.Ltosol_format.load ltosol_filename)
  in
  let id_stamp_counters =
    Flambda2_reaper.Ltosol_format.id_stamp_counters ltosol
  in
  Flambda2_reaper.Id_stamp_counters.restore_for_resume id_stamp_counters;
  let solution =
    Profile.record_call ~accumulate:true "ltosol_deserialise" (fun () ->
        Flambda2_reaper.Ltosol_format.solution_for_members ltosol
          ~members:batch_members)
  in
  let participant_units =
    Compilation_unit.Set.of_list
      (Flambda2_reaper.Ltosol_format.participants ltosol)
  in
  let get_module_info comp_unit =
    if Compilation_unit.Set.mem comp_unit participant_units
    then
      Misc.fatal_errorf
        "-reaper-rebuild: attempted to read the .cmx of %a, which participated \
         in the solve"
        (Format_doc.compat Compilation_unit.print)
        comp_unit;
    get_module_info comp_unit
  in
  let cmx_loader = Flambda_cmx.create_loader ~get_module_info in
  fun ~keep_symbol_tables
    ~cmx_filename
    ~(paused_unit_infos : Cmx_format.unit_infos)
    ~ppf_dump:_
    ~prefixname:_
  ->
    let header, export_info =
      read_lto_header_and_export_info ~filename:cmx_filename paused_unit_infos
    in
    (* We expect the stamp counters in the .cmx file to be less than the
       counters in the .ltosol file, because the -reaper-solve invocation begins
       by taking the maximum counters across the .cmx files it reads. Therefore,
       we can ignore these counters. *)
    if
      Flambda2_reaper.Id_stamp_counters.any_greater_than
        (Flambda2_reaper.Lto_sections.Header.id_stamp_counters header)
        id_stamp_counters
    then
      Misc.fatal_error
        "The rebuild data contains ID stamp counters greater than those in the \
         the solution file. Stamp counter monotonicity is broken.";
    let unit_metadata, rebuild_data =
      Profile.record_call ~accumulate:true "lto_sections_deserialise" (fun () ->
          Flambda2_reaper.Lto_sections.read_for_rebuild ~filename:cmx_filename
            ~sections:paused_unit_infos.ui_file_sections ~export_info header)
    in
    (* CR mvellacott: add debug printing code. *)
    (* Code metadata of the participants comes from the solution and that of
       other units from their .cmx files, loaded on demand. *)
    let flambda, all_code, _final_typing_env, free_names =
      Flambda2_reaper.Reaper.Staged.rebuild ~unit_metadata
        ~traverse_rebuild:rebuild_data ~solution ~typing:None ~machine_width
        ~cmx_loader ~all_code:Exported_code.empty
    in
    (* Reaped CMXs are only used for linking, so leave their Flambda export
       information empty, as for opaque compilation. The backend still needs the
       rebuilt code metadata and solved closure offsets. *)
    let offsets =
      Flambda2_reaper.Rebuild_solution.offsets_for_free_names solution
        free_names
    in
    let reachable_names =
      NO.singleton_symbol
        (Flambda_unit.module_symbol flambda)
        Flambda2_nominal.Name_mode.normal
    in
    Compiler_hooks.execute Reaped_flambda2 flambda;
    (* CR mvellacott: in the future we'd like to always localise unreachable
       symbols, but it can cause issues with LTO if not properly handled. *)
    flambda_result_to_cmm ~keep_symbol_tables
      ~localise_unreachable_symbols:false
      { flambda; all_code; offsets; reachable_names }
