(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*                 Ulysse Gérard, Thomas Refis, Tarides                   *)
(*                    Nathanaëlle Courant, OCamlPro                       *)
(*              Gabriel Scherer, projet Picube, INRIA Paris               *)
(*                                                                        *)
(*   Copyright 2021 Institut National de Recherche en Informatique et     *)
(*     en Automatique.                                                    *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

module Layout = Jkind_types.Sort.Const

open Shape

type result =
  | Resolved of Uid.t
  | Resolved_alias of Uid.t * result
  | Unresolved of t
  | Approximated of Uid.t option
  | Internal_error_missing_uid

let rec print_result fmt result =
  match result with
  | Resolved uid ->
      Format.fprintf fmt "@[Resolved: %a@]@;" Uid.print uid
  | Resolved_alias (uid, r) ->
      Format.fprintf fmt "@[Alias: %a -> %a@]@;"
        Uid.print uid print_result r
  | Unresolved shape ->
      Format.fprintf fmt "@[Unresolved: %a@]@;" print shape
  | Approximated (Some uid) ->
      Format.fprintf fmt "@[Approximated: %a@]@;" Uid.print uid
  | Approximated None ->
      Format.fprintf fmt "@[Approximated: No uid@]@;"
  | Internal_error_missing_uid ->
      Format.fprintf fmt "@[Missing uid@]@;"


let find_shape env id =
  let namespace = Shape.Sig_component_kind.Module in
  Env.shape_of_path ~namespace env (Pident id)

module Make(Params : sig
  val fuel : int
  val read_unit_shape : unit_name:string -> t option
  val type_shape_compression : bool
  val lookup_shape_for_uid : Uid.t -> t option
end) = struct
  (* We implement a strong call-by-need reduction, following an
     evaluator from Nathanaelle Courant. *)

  type nf = { uid: Uid.t option; desc: nf_desc; approximated: bool }
  and nf_desc =
    | NVar of var
    | NApp of nf * nf
    | NAbs of local_env * var * t * delayed_nf
    | NStruct of delayed_nf Item.Map.t
    | NAlias of delayed_nf
    | NProj of nf * Item.t
    | NLeaf
    | NType_decl of delayed_nf_tds
    | NComp_unit of string
    | NError of string

  (* A type of normal forms for strong call-by-need evaluation.
     The normal form of an abstraction
       Abs(x, t)
     is a closure
       NAbs(env, x, t, dnf)
     when [env] is the local environment, and [dnf] is a delayed
     normal form of [t].

     A "delayed normal form" is morally equivalent to (nf Lazy.t), but
     we use a different representation that is compatible with
     memoization (lazy values are not hashable/comparable by default
     comparison functions): we represent a delayed normal form as
     just a not-yet-computed pair [local_env * t] of a term in a
     local environment -- we could also see this as a term under
     an explicit substitution. This delayed thunked is "forced"
     by calling the normalization function as usual, but duplicate
     computations are precisely avoided by memoization.
   *)
  and delayed_nf = Thunk of local_env * t

  and delayed_nf_tds = Thunk_tds of local_env * tds

  and local_env =
    { env: delayed_nf option Ident.Map.t;
      uid_renaming : Shape.Uid.t Shape.Uid.Map.t }
  (* When reducing in the body of an abstraction [Abs(x, body)], we
     bind [x] to [None] in the environment. [Some v] is used for
     actual substitutions, for example in [App(Abs(x, body), t)], when
     [v] is a thunk that will evaluate to the normal form of [t]. *)

  let approx_nf nf = { nf with approximated = true }

  let rec equal_local_env t1 t2 =
    Ident.Map.equal (Option.equal equal_delayed_nf) t1.env t2.env &&
    Shape.Uid.Map.equal (Shape.Uid.equal) t1.uid_renaming t2.uid_renaming

  and equal_delayed_nf t1 t2 =
    match t1, t2 with
    | Thunk (l1, t1), Thunk (l2, t2) ->
      if equal t1 t2 then equal_local_env l1 l2
      else false

  and equal_delayed_nf_tds t1 t2 =
    match t1, t2 with
    | Thunk_tds (l1, t1), Thunk_tds (l2, t2) ->
      if Shape.equal_tds t1 t2 then equal_local_env l1 l2
      else false

  and equal_nf_desc d1 d2 =
    match d1, d2 with
    | NVar v1, NVar v2 -> Ident.equal v1 v2
    | NAbs (l1, v1, t1, nf1), NAbs (l2, v2, t2, nf2) ->
      if not (Ident.equal v1 v2) then false
      else if not (equal t1 t2) then false
      else if not (equal_delayed_nf nf1 nf2) then false
      else equal_local_env l1 l2
    | NApp (v1, t1), NApp (v2, t2) ->
      if equal_nf v1 v2 then equal_nf t1 t2
      else false
    | NLeaf, NLeaf -> true
    | NType_decl tds1, NType_decl tds2 -> equal_delayed_nf_tds tds1 tds2
    | NStruct t1, NStruct t2 ->
      Item.Map.equal equal_delayed_nf t1 t2
    | NProj (t1, i1), NProj (t2, i2) ->
      if Item.compare i1 i2 <> 0 then false
      else equal_nf t1 t2
    | NComp_unit c1, NComp_unit c2 -> String.equal c1 c2
    | NAlias a1, NAlias a2 -> equal_delayed_nf a1 a2
    | NError e1, NError e2 -> String.equal e1 e2
    | NVar _, (NLeaf | NApp _ | NAbs _ | NStruct _ | NProj _ | NComp_unit _ | NAlias _ | NError _ | NType_decl _)
    | NLeaf, (NVar _ | NApp _ | NAbs _ | NStruct _ | NProj _ | NComp_unit _ | NAlias _ | NError _ | NType_decl _)
    | NApp _, (NVar _ | NLeaf | NAbs _ | NStruct _ | NProj _ | NComp_unit _ | NAlias _ | NError _ | NType_decl _)
    | NAbs _, (NVar _ | NLeaf | NApp _ | NStruct _ | NProj _ | NComp_unit _ | NAlias _ | NError _ | NType_decl _)
    | NStruct _, (NVar _ | NLeaf | NApp _ | NAbs _ | NProj _ | NComp_unit _ | NAlias _ | NError _ | NType_decl _)
    | NProj _, (NVar _ | NLeaf | NApp _ | NAbs _ | NStruct _ | NComp_unit _ | NAlias _ | NError _ | NType_decl _)
    | NComp_unit _, (NVar _ | NLeaf | NApp _ | NAbs _ | NStruct _ | NProj _ | NAlias _ | NError _ | NType_decl _)
    | NAlias _, (NVar _ | NLeaf | NApp _ | NAbs _ | NStruct _ | NProj _ | NComp_unit _ | NError _ | NType_decl _)
    | NError _, (NVar _ | NLeaf | NApp _ | NAbs _ | NStruct _ | NProj _ | NComp_unit _ | NAlias _ | NType_decl _)
    | NType_decl _, (NVar _ | NLeaf | NApp _ | NAbs _ | NStruct _ | NProj _ | NComp_unit _ | NAlias _ | NError _)
    -> false

  and equal_nf t1 t2 =
    if not (Option.equal Uid.equal t1.uid t2.uid) then false
    else equal_nf_desc t1.desc t2.desc

  module ReduceMemoTable = Hashtbl.Make(struct
      type nonrec t = local_env * t

      let hash t = Hashtbl.hash t

      let equal (env1, t1) (env2, t2) =
        if equal t1 t2 then equal_local_env env1 env2
        else false
  end)

  module ReadBackMemoTable = Hashtbl.Make(struct
      type nonrec t = nf

      let hash t = Hashtbl.hash t

  let equal a b = equal_nf a b
  end)

  let in_reduce_memo_table memo_table memo_key f arg =
    match ReduceMemoTable.find memo_table memo_key with
        | res -> res
    | exception Not_found ->
        let res = f arg in
        ReduceMemoTable.replace memo_table memo_key res;
        res

  let in_read_back_memo_table memo_table memo_key f arg =
    match ReadBackMemoTable.find memo_table memo_key with
    | res -> res
    | exception Not_found ->
        let res = f arg in
        ReadBackMemoTable.replace memo_table memo_key res;
        res

  type env = {
    fuel: int ref;
    global_env: Env.t;
    local_env: local_env;
    reduce_memo_table: nf ReduceMemoTable.t;
    read_back_memo_table: t ReadBackMemoTable.t;
  }

  let bind env var shape =
    { env with local_env =
      { env = Ident.Map.add var shape env.local_env.env;
        uid_renaming = env.local_env.uid_renaming} }

  let bind_new_uid env uid new_uid =
    let local_env = { env.local_env with
      uid_renaming = Shape.Uid.Map.add uid new_uid env.local_env.uid_renaming }
    in
    { env with local_env }

  let rec reduce_ env t =
    let local_env = env.local_env in
    let memo_key = (local_env, t) in
    in_reduce_memo_table env.reduce_memo_table memo_key (reduce__ env) t
  (* Memoization is absolutely essential for performance on this
     problem, because the normal forms we build can in some real-world
     cases contain an exponential amount of redundancy. Memoization
     can avoid the repeated evaluation of identical subterms,
     providing a large speedup, but even more importantly it
     implicitly shares the memory of the repeated results, providing
     much smaller normal forms (that blow up again if printed back
     as trees). A functor-heavy file from Irmin has its shape normal
     form decrease from 100Mio to 2.5Mio when memoization is enabled.

     Note: the local environment is part of the memoization key, while
     it is defined using a type Ident.Map.t of non-canonical balanced
     trees: two maps could have exactly the same items, but be
     balanced differently and therefore hash differently, reducing
     the effectivenss of memoization.
     This could in theory happen, say, with the two programs
       (fun x -> fun y -> ...)
     and
       (fun y -> fun x -> ...)
     having "the same" local environments, with additions done in
     a different order, giving non-structurally-equal trees. Should we
     define our own hash functions to provide robust hashing on
     environments?

     We believe that the answer is "no": this problem does not occur
     in practice. We can assume that identifiers are unique on valid
     typedtree fragments (identifier "stamps" distinguish
     binding positions); in particular the two program fragments above
     in fact bind *distinct* identifiers x (with different stamps) and
     different identifiers y, so the environments are distinct. If two
     environments are structurally the same, they must correspond to
     the evaluation environments of two sub-terms that are under
     exactly the same scope of binders. So the two environments were
     obtained by the same term traversal, adding binders in the same
     order, giving the same balanced trees: the environments have the
     same hash.
  *)

  and force env (Thunk (local_env, t)) =
    reduce_ { env with local_env } t

  and reduce__
    ({fuel; global_env; local_env; _} as env) (t : t) =
    let reduce env t = reduce_ env t in
    let delay_reduce env t = Thunk (env.local_env, t) in
    let delay_reduce_tds env tds = Thunk_tds (env.local_env, tds) in
    let return desc = { uid = t.uid; desc; approximated = t.approximated } in
    let rec force_aliases nf = match nf.desc with
      | NAlias delayed_nf ->
          let nf = force env delayed_nf in
          force_aliases nf
      | _ -> nf
    in
    let reset_uid_if_new_binding t' =
      match t.uid with
      | None -> t'
      | Some _ as uid -> { t' with uid }
    in
    if !fuel < 0 then approx_nf (return (NError "NoFuelLeft"))
    else
      match t.desc with
      | Comp_unit unit_name ->
          begin match Params.read_unit_shape ~unit_name with
          | Some t -> reduce env t
          | None -> return (NComp_unit unit_name)
          end
      | App(f, arg) ->
          let f = reduce env f |> force_aliases in
          begin match f.desc with
          | NAbs(clos_env, var, body, _body_nf) ->
              let arg = delay_reduce env arg in
              let env = bind { env with local_env = clos_env } var (Some arg) in
              reduce env body |> reset_uid_if_new_binding
          | _ ->
              let arg = reduce env arg in
              return (NApp(f, arg))
          end
      | Proj(str, item) ->
          let str = reduce env str |> force_aliases in
          let nored () = return (NProj(str, item)) in
          begin match str.desc with
          | NStruct (items) ->
              begin match Item.Map.find item items with
              | exception Not_found -> nored ()
              | nf -> force env nf |> reset_uid_if_new_binding
              end
          | _ ->
              nored ()
          end
      | Abs(var, body) ->
          let body_nf = delay_reduce (bind env var None) body in
          return (NAbs(local_env, var, body, body_nf))
      | Var id ->
          begin match Ident.Map.find id local_env.env with
          (* Note: instead of binding abstraction-bound variables to
             [None], we could unify it with the [Some v] case by
             binding the bound variable [x] to [NVar x].

             One reason to distinguish the situations is that we can
             provide a different [Uid.t] location; for bound
             variables, we use the [Uid.t] of the bound occurrence
             (not the binding site), whereas for bound values we use
             their binding-time [Uid.t]. *)
          | None -> return (NVar id)
          | Some def ->
              begin match force env def with
              | { uid = Some _; _  } as nf -> nf
                  (* This var already has a binding uid *)
              | { uid = None; _ } as nf -> { nf with uid = t.uid }
                  (* Set the var's binding uid *)
              end
          | exception Not_found ->
          match find_shape global_env id with
          | exception Not_found -> return (NVar id)
          | res when res = t -> return (NVar id)
          | res ->
              decr fuel;
              reduce env res
          end
      | Leaf ->
        (match t.uid  with
        | None -> return NLeaf
        | Some uid ->
          match Shape.Uid.Map.find_opt uid env.local_env.uid_renaming with
          | Some new_uid ->
            { uid = Some new_uid; desc = NLeaf; approximated = t.approximated }
          | None ->
            match Params.lookup_shape_for_uid uid with
            | Some sh -> reduce__ env sh
            | None -> return NLeaf)
      | Type_decl tds ->
        let env, uid = match t.uid with
        | None -> env, None
        | Some uid ->
            (* CR sspies: Consider the case of internal uids. *)
            let new_uid = Shape.Uid.mk ~current_unit:None in
            bind_new_uid env uid new_uid, Some new_uid
        in
        { desc = (NType_decl (delay_reduce_tds env tds));
          uid; approximated = t.approximated }
      | Struct m ->
          let mnf = Item.Map.map (delay_reduce env) m in
          return (NStruct mnf)
      | Alias t -> return (NAlias (delay_reduce env t))
      | Error s -> approx_nf (return (NError s))

  and read_back env (nf : nf) : t =
  in_read_back_memo_table env.read_back_memo_table nf (read_back_ env) nf
  (* The [nf] normal form we receive may contain a lot of internal
     sharing due to the use of memoization in the evaluator. We have
     to memoize here again, otherwise the sharing is lost by mapping
     over the term as a tree. *)

  and read_back_ env (nf : nf) : t =
    read_back_desc ~uid:nf.uid env nf.desc

  and read_back_desc ~uid env desc =
    let read_back nf = read_back env nf in
    let read_back_force dnf = read_back (force env dnf) in
    match desc with
    | NVar v ->
      var (Option.get uid) v
    | NApp (nft, nfu) ->
        let f = read_back nft in
        let arg = read_back nfu in
        app ?uid f ~arg
    | NAbs (_env, x, _t, nf) ->
      let body = read_back_force nf in
      abs ?uid x body
    | NStruct nstr ->
      let map = Item.Map.map read_back_force nstr in
      str ?uid map
    | NProj (nf, item) ->
        let t = read_back nf in
        proj ?uid t item
    | NLeaf -> leaf' uid
    | NComp_unit s -> comp_unit ?uid s
    | NAlias nf -> alias ?uid (read_back_force nf)
    | NError t -> error ?uid t
    | NType_decl tds ->
      type_decl uid (read_back_tds env tds)

  and read_back_tds env (tds: delayed_nf_tds) : tds =
    let Thunk_tds (l, tds) = tds in
    let env = { env with local_env = l } in
    force_reduce_tds env tds

  (* CR sspies: We currently do not match the delayed reduction strategy for
     type declarations that is used for the other parts, and instead
     aggressively reduce the occurrences of shapes in type declarations. *)
  and force_reduce_tds env ({definition; type_params}: tds) =
    let def = match definition with
    | Tds_other -> Tds_other
    | Tds_alias sh ->
      Tds_alias (force_reduce_ts env sh)
    | Tds_variant { simple_constructors; complex_constructors } ->
      Tds_variant {
        simple_constructors;
        complex_constructors =
          List.map
            (Shape.complex_constructor_map
              (fun (sh, ly) -> force_reduce_ts env sh, ly)
            )
          complex_constructors
      }
    | Tds_variant_unboxed { name; arg_name; arg_shape; arg_layout } ->
      Tds_variant_unboxed { name; arg_name;
        arg_shape = force_reduce_ts env arg_shape; arg_layout }
    | Tds_record { fields; kind } ->
      Tds_record { fields = List.map (fun (name, sh, ly) ->
                                          name, force_reduce_ts env sh, ly)
                                fields
                 ; kind }
    in
    (* CR sspies: Does it even make sense to reduce in the type params? *)
    { definition = def; type_params = List.map (force_reduce_ts env) type_params }

  and force_reduce_ts env (ts: 'a ts) =
    match ts with
    | Ts_constr ((sh, ly), args) ->
      Ts_constr ((read_back env (reduce__ env sh), ly), args)
    | Ts_tuple ts -> Ts_tuple (List.map (force_reduce_ts env) ts)
    | Ts_unboxed_tuple ts -> Ts_unboxed_tuple (List.map (force_reduce_ts env) ts)
    | Ts_var (name, ly) -> Ts_var (name, ly)
    | Ts_predef (predef, ts) -> Ts_predef (predef, List.map (force_reduce_ts env) ts)
    | Ts_arrow (arg, ret) -> Ts_arrow (force_reduce_ts env arg, force_reduce_ts env ret)
    | Ts_variant (fields) ->
      let fields = Shape.poly_variant_constructors_map (force_reduce_ts env) fields in
      Ts_variant fields
    | Ts_other ly -> Ts_other ly

let rec used_uids_shape (sh : Shape.t) =
  match sh.desc with
  | Comp_unit _ -> Shape.Uid.Set.empty
  | Var _ -> Shape.Uid.Set.empty
  | Leaf -> (
    (* After reduction, the only possible occurrences of uids are the leafs. *)
    match sh.uid with
    | None -> Shape.Uid.Set.empty
    | Some uid -> Shape.Uid.Set.singleton uid)
  | Type_decl tds -> used_uids_tds tds
  | Abs (_, e) -> used_uids_shape e
  | App (f, s) -> Shape.Uid.Set.union (used_uids_shape f) (used_uids_shape s)
  | Struct items ->
    Shape.Item.Map.fold
      (fun _ sh acc -> Shape.Uid.Set.union (used_uids_shape sh) acc)
      items Shape.Uid.Set.empty
  | Alias sh -> used_uids_shape sh
  | Proj (str, _) -> used_uids_shape str
  | Error _ -> Shape.Uid.Set.empty

and used_uids_tds (tds : Shape.tds) =
  match tds.definition with
  | Tds_other -> Shape.Uid.Set.empty
  | Tds_alias sh -> used_uids_ts sh
  | Tds_record { fields; _ } ->
    List.fold_left
      (fun acc (_, sh, _) -> Shape.Uid.Set.union (used_uids_ts sh) acc)
      Shape.Uid.Set.empty fields
  | Tds_variant { complex_constructors; _ } ->
    List.fold_left
      (fun acc { Shape.args; _ } ->
        Shape.Uid.Set.union
          (List.fold_left
             (fun acc { Shape.field_value = ts, _; _ } ->
               Shape.Uid.Set.union (used_uids_ts ts) acc)
             Shape.Uid.Set.empty args)
          acc)
      Shape.Uid.Set.empty complex_constructors
  | Tds_variant_unboxed { arg_shape; _ } -> used_uids_ts arg_shape

and used_uids_ts (ts : 'a Shape.ts) =
  let used_uids_type_shapes tss =
    List.fold_left
      (fun acc sh -> Shape.Uid.Set.union (used_uids_ts sh) acc)
      Shape.Uid.Set.empty tss
  in
  match ts with
  | Ts_constr ((sh, _), args) ->
    Shape.Uid.Set.union (used_uids_shape sh) (used_uids_type_shapes args)
  | Ts_tuple ts -> used_uids_type_shapes ts
  | Ts_unboxed_tuple ts -> used_uids_type_shapes ts
  | Ts_var _ -> Shape.Uid.Set.empty
  | Ts_predef _ -> Shape.Uid.Set.empty
  | Ts_arrow (arg, ret) -> used_uids_type_shapes [arg; ret]
  | Ts_variant fields ->
    List.fold_left
      (fun acc { Shape.pv_constr_args; _ } ->
        Shape.Uid.Set.union (used_uids_type_shapes pv_constr_args) acc)
      Shape.Uid.Set.empty fields
  | Ts_other _ -> Shape.Uid.Set.empty

(* We compress the cases where of the form
    [Shape.type_def (Tds_alias (Ts_constr (...)))]
  if the UID of the shape is not later used as part of a recursive definition.
*)
let rec compress_shape (used_uids : Shape.Uid.Set.t) (sh : Shape.t) =
  let uid_used = function
    | None -> false
    | Some uid -> Shape.Uid.Set.mem uid used_uids
  in
  let compressed =
    match[@warning "-4"] sh.desc with
    | Comp_unit _ | Var _ | Proj _ | Leaf | Abs _ | App _ | Struct _ | Error _
      ->
      sh
    | Alias sh -> compress_shape used_uids sh
    | Type_decl { definition = Tds_alias (Ts_constr ((inner_sh, _), [])); _ }
      when not (uid_used sh.uid) ->
      compress_shape used_uids inner_sh
    | Type_decl tds ->
      let uid = if uid_used sh.uid then sh.uid else None in
      Shape.type_decl uid (compress_tds used_uids tds)
  in
  compressed

and compress_tds (used_uids : Shape.Uid.Set.t) (tds : Shape.tds) =
  let desc =
    match tds.definition with
    | Tds_other -> Shape.Tds_other
    | Tds_alias sh -> Shape.Tds_alias (compress_ts used_uids sh)
    | Tds_record { fields; kind } ->
      Shape.Tds_record
        { fields =
            List.map
              (fun (name, sh, layout) -> name, compress_ts used_uids sh, layout)
              fields;
          kind
        }
    | Tds_variant { simple_constructors; complex_constructors } ->
      Shape.Tds_variant
        { simple_constructors;
          complex_constructors =
            List.map
              (Shape.complex_constructor_map (fun (sh, ly) ->
                   compress_ts used_uids sh, ly))
              complex_constructors
        }
    | Tds_variant_unboxed { name; arg_name; arg_shape; arg_layout; _ } ->
      Shape.Tds_variant_unboxed
        { name;
          arg_name;
          arg_shape = compress_ts used_uids arg_shape;
          arg_layout
        }
  in
  let params = List.map (fun sh -> compress_ts used_uids sh) tds.type_params in
  { Shape.definition = desc; type_params = params }

and compress_ts (used_uids : Shape.Uid.Set.t) (ts : 'a Shape.ts) =
  let compress_ts_list tss =
    List.map (fun ts -> compress_ts used_uids ts) tss
  in
  match ts with
  | Ts_constr ((sh, ly), args) ->
    Shape.Ts_constr ((compress_shape used_uids sh, ly), compress_ts_list args)
  | Ts_tuple ts -> Shape.Ts_tuple (compress_ts_list ts)
  | Ts_unboxed_tuple ts -> Shape.Ts_unboxed_tuple (compress_ts_list ts)
  | Ts_var _ -> ts
  | Ts_predef _ -> ts
  | Ts_arrow (arg, ret) ->
    Shape.Ts_arrow (compress_ts used_uids arg, compress_ts used_uids ret)
  | Ts_variant fields ->
    let fields =
      Shape.poly_variant_constructors_map (compress_ts used_uids) fields
    in
    Shape.Ts_variant fields
  | Ts_other _ -> ts

  (* Sharing the memo tables is safe at the level of a compilation unit since
    idents should be unique *)
  let reduce_memo_table = Local_store.s_table ReduceMemoTable.create 42
  let read_back_memo_table = Local_store.s_table ReadBackMemoTable.create 42

  let reduce global_env t =
    let fuel = ref Params.fuel in
    let local_env = { env = Ident.Map.empty;
                      uid_renaming = Shape.Uid.Map.empty }
    in
    let maybe_compress_shape sh =
      if Params.type_shape_compression
      then
        let used_uids = used_uids_shape sh in
        compress_shape used_uids sh
      else sh
    in
    let env = {
      fuel;
      global_env;
      reduce_memo_table = !reduce_memo_table;
      read_back_memo_table = !read_back_memo_table;
      local_env;
    } in
     reduce_ env t
  |> read_back env
  |> maybe_compress_shape

  let reduce_tds global_env tds =
    match reduce global_env (Shape.type_decl None tds) with
    | { desc = Shape.Type_decl tds; _ } -> tds
    | { desc = Shape.Leaf; _ } as s ->
      { definition = Tds_alias (Ts_constr ((s, Layout_to_be_determined), [])); type_params = [] }
    | s -> Misc.fatal_errorf "Should reduce to type declaration, but found %a."
            Shape.print s

  let reduce_ts global_env ts =
    let tds = reduce_tds global_env { definition = Tds_alias ts; type_params = [] }
    in Ts_constr ((Shape.type_decl None tds, Layout_to_be_determined), [])


  let rec is_stuck_on_comp_unit (nf : nf) =
    match nf.desc with
    | NVar _ ->
        (* This should not happen if we only reduce closed terms *)
        false
    | NApp (nf, _) | NProj (nf, _) -> is_stuck_on_comp_unit nf
    | NStruct _ | NAbs _ -> false
    | NAlias _ -> false
    | NComp_unit _ -> true
    | NError _ -> false
    | NLeaf -> false
    | NType_decl _ -> false

  let rec reduce_aliases_for_uid env (nf : nf) =
    match nf with
    | { uid = Some uid; desc = NAlias dnf; approximated = false; _ } ->
        let result = reduce_aliases_for_uid env (force env dnf) in
        Resolved_alias (uid, result)
    | { uid = Some uid; approximated = false; _ } -> Resolved uid
    | { uid; approximated = true } -> Approximated uid
    | { uid = None; approximated = false; _ } ->
      (* A missing Uid after a complete reduction means the Uid was first
         missing in the shape which is a code error. Having the
         [Missing_uid] reported will allow Merlin (or another tool working
         with the index) to ask users to report the issue if it does happen.
      *)
      Internal_error_missing_uid

  let reduce_for_uid global_env t =
    let fuel = ref Params.fuel in
    let local_env = { env = Ident.Map.empty;
                      uid_renaming = Shape.Uid.Map.empty }
    in
    let env = {
      fuel;
      global_env;
      reduce_memo_table = !reduce_memo_table;
      read_back_memo_table = !read_back_memo_table;
      local_env;
    } in
    let nf = reduce_ env t in
    if is_stuck_on_comp_unit nf then
      Unresolved (read_back env nf)
    else
      reduce_aliases_for_uid env nf
end

module Local_reduce =
  Make(struct
    let fuel = 10
    let read_unit_shape ~unit_name:_ = None
    let type_shape_compression = false
    let lookup_shape_for_uid _ = None
  end)

let local_reduce = Local_reduce.reduce
let local_reduce_for_uid = Local_reduce.reduce_for_uid
let local_reduce_tds = Local_reduce.reduce_tds
let local_reduce_ts = Local_reduce.reduce_ts
